prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix owl: <http://www.w3.org/2002/07/owl#>
prefix skos: <http://www.w3.org/2004/02/skos/core#>
prefix bf2: <http://id.loc.gov/ontologies/bibframe/>
prefix madsrdf: <http://www.loc.gov/mads/rdf/v1#>
prefix : <https://id.kb.se/vocab/>

construct {
    ?s ?p ?o ;
        skos:prefLabel ?enLabel, ?fiLabel, ?deLabel, ?frLabel .

} where {
    graph <https://id.kb.se/dataset/relators> {
        {
            ?s ?p ?o .
        } union {
            ?s skos:exactMatch ?lc .
            graph <http://id.loc.gov/vocabulary/relators> {
                ?lc madsrdf:authoritativeLabel|skos:prefLabel ?lcLabel .
                bind(strlang(?lcLabel, 'en') as ?enLabel)
            }
        } union {
            ?s skos:exactMatch ?mts .
            graph <http://urn.fi/URN:NBN:fi:au:mts:> {
                ?mts skos:prefLabel ?fiLabel .
                filter(lang(?fiLabel) = 'fi')
            }
        } union {
            ?s skos:exactMatch ?dnb .
            graph <https://d-nb.info/standards/elementset/gnd> {
                ?dnb rdfs:label ?deLabel .
                filter(lang(?deLabel) = 'de')
            }
        } union {
            ?s skos:exactMatch ?bnf .
            graph <sparql/bnf-roles> {
                ?bnf skos:prefLabel ?frLabel .
                filter(lang(?frLabel) = 'fr')
            }
        }
    }
}
