prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix owl: <http://www.w3.org/2002/07/owl#>
prefix skos: <http://www.w3.org/2004/02/skos/core#>

construct {
    ?idrel a owl:ObjectProperty;
        skos:notation ?notation;
        ?p ?o;
        owl:equivalentProperty ?locrel;
        rdfs:subPropertyOf ?superrel .
} where {
    graph <http://id.kb.se/dataset/relators> {
        {
            ?idrel ?p ?o .
        } union {
            ?idrel skos:notation ?notation .
            graph <http://id.loc.gov/vocabulary/relators> {
                ?locrel skos:notation ?notation .
                optional {
                    ?locrel rdfs:subPropertyOf ?locsuperrel .
                    optional {
                        ?locsuperrel skos:notation ?supernot .
                        graph <http://id.kb.se/dataset/relators> {
                            ?idsuperrel skos:notation ?supernot .
                        }
                    }
                    bind(coalesce(?idsuperrel, ?locsuperrel) as ?superrel)
                }
            }
        }
    }
}
