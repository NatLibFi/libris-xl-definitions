prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix skos: <http://www.w3.org/2004/02/skos/core#>
prefix madsrdf: <http://www.loc.gov/mads/rdf/v1#>
prefix dc: <http://purl.org/dc/terms/>
prefix iso639_2: <http://id.loc.gov/vocabulary/iso639-2/>
prefix kbv: <https://id.lkd.local/vocab/>

construct {
    ?s ?p ?o ;
        skos:prefLabel ?prefLabel ;
        skos:altLabel ?altLabel ;
        skos:exactMatch ?locLang ;
} where {
    graph <https://id.lkd.local/dataset/languages> {
        {
            ?s ?p ?o .
        } union {
            ?s skos:notation ?locCode .
            filter(
                datatype(?locCode) = kbv:ISO639-2-T ||
                datatype(?locCode) = xsd:string
            )
            bind(iri(concat(str(iso639_2:), str(?locCode))) as ?locLang)
            graph <http://id.loc.gov/vocabulary/iso639-2> {
                {
                    filter(!contains(?prefLabel, "|"))
                    ?locLang madsrdf:authoritativeLabel|skos:prefLabel ?prefLabel
                } union {
                    filter(contains(?prefLabels, "|"))
                    ?locLang madsrdf:authoritativeLabel|skos:prefLabel ?prefLabels
                    bind(replace(?prefLabels, " *\\|.*$", "") as ?prefLabel)
                    bind(replace(?prefLabels, "^[^|]+\\| *", "") as ?altLabels)
                    values ?n { 1 2 3 4 5 6 7 }
                    bind(concat("^([^|]+\\|){", str(?n) ,"} *") as ?skipN)
                    bind(replace(replace(?altLabels, ?skipN, ""), " *\\|.*$", "") as ?altLabel)
                }
            }
        }
    }
}
