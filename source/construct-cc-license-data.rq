PREFIX cc: <http://creativecommons.org/ns#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX frbr: <http://purl.org/vocab/frbr/core#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xhv: <http://www.w3.org/1999/xhtml/vocab#>

CONSTRUCT {
    ?http a cc:License ;
        owl:sameAs ?s ;
        ?p ?o ;
        dct:identifier ?identifier .
} WHERE {
    VALUES (?p) {
        (dct:creator) (dct:title) (xhv:license) (cc:permits) (cc:requires) (cc:prohibits)
    }
    GRAPH ?g {
        ?license a cc:License ; dct:identifier ?id .
        BIND(STR(?id) AS ?idstr)
        BIND(REPLACE(?idstr, "^\\s*\\(|\\)\\s*$", "") AS ?identifier)
        OPTIONAL { ?license ?p ?o }
    }
    {
        SELECT ?real ?idstr {
            GRAPH ?real { ?x dct:identifier ?id }
            FILTER(!REGEX(STR(?real), ".*/deed\\.\\w+$"))
            BIND(STR(?id) AS ?idstr)
        }
    }
    BIND(COALESCE(?real, ?g, ?license) AS ?s)
    BIND(IF(STRSTARTS(STR(?s), "https://"),
            IRI(CONCAT("http://", STRAFTER((STR(?s)), "https://"))),
            ?s) AS ?http)
}
