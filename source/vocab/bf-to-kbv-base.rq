prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix owl: <http://www.w3.org/2002/07/owl#>
prefix bf: <http://id.loc.gov/ontologies/bibframe/>
prefix kbv: <https://id.kb.se/vocab/>

construct {

    ?kbvterm a ?type ;
        ?maplink ?bfterm ;
        rdfs:label ?kbvlabel_en ;
        rdfs:subClassOf ?kbvbaseclass ;
        rdfs:domain ?kbvdomain ;
        rdfs:range ?kbvrange ;
        rdfs:isDefinedBy kbv: .

} where {

    # General

    ?bfterm a ?type .
    filter ((?bfterm != bf:) && strstarts(str(?bfterm), str(bf:)))
    ?bfterm rdfs:label ?bflabel .

    bind(if(?type = owl:Class, owl:equivalentClass, owl:equivalentProperty) as ?maplink)

    optional {
        ?mappedterm ?maplink ?bfterm
        optional {
            ?mappedterm rdfs:label ?mappedlabel .
            filter(langMatches(?mappedlabel, 'en'))
        }
    }

    bind(coalesce(?mappedterm,
                IRI(concat(replace(str(?bfterm), str(bf:), str(kbv:)))))
            as ?kbvterm)

    bind(coalesce(?mappedlabel, strlang(?bflabel, 'en')) as ?kbvlabel_en)


    # Class

    optional {
        ?bfterm rdfs:subClassOf ?baseclass .

        optional { ?mappedclass owl:equivalentClass ?baseclass }

        bind(coalesce(?mappedclass,
                    IRI(concat(replace(str(?baseclass), str(bf:), str(kbv:)))))
                as ?kbvbaseclass)
    }


    # Property

    optional {
        ?bfterm rdfs:domain ?domain
        optional { ?mappeddomain owl:equivalentClass ?domain }

        bind(coalesce(?mappeddomain,
                    IRI(concat(replace(str(?domain), str(bf:), str(kbv:)))))
                as ?kbvdomain)
    }
    optional {
        filter exists { ?bfterm rdfs:comment "Used with Work, Instance or Item" }
        bind(kbv:Endeavour as ?kbvdomain)
    }
    optional {
        filter exists { ?bfterm rdfs:comment "Used with Work or Instance" }
        bind(kbv:Creation as ?kbvdomain)
    }
    optional {
        filter exists { ?bfterm rdfs:comment "Used with Unspecified" }
        bind(rdfs:Resource as ?kbvdomain)
    }

    optional {
        ?bfterm rdfs:range ?range
        optional { ?mappedrange owl:equivalentClass ?range }
        bind(coalesce(?mappedrange,
                    IRI(concat(replace(str(?range), str(bf:), str(kbv:)))))
                as ?kbvrange)
    }
    optional {
        filter exists { ?bfterm rdfs:comment "Expected value Work, Instance or Item" }
        bind(kbv:Endeavour as ?kbvrange)
    }
    optional {
        filter exists { ?bfterm rdfs:comment "Expected value Work or Instance" }
        bind(kbv:Creation as ?kbvrange)
    }

}
