prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix owl: <http://www.w3.org/2002/07/owl#>
prefix skos: <http://www.w3.org/2004/02/skos/core#>

construct {

    ?s ?p ?o ;
        rdfs:label ?lcpreflabel ;
        skos:prefLabel ?rdapreflabel ;
        skos:definition ?rdadefinition .

} where {
    {
        graph <https://id.kb.se/dataset/rdaterms> {
            ?s ?p ?o .
        }
    } union {
        ?s skos:exactMatch ?lc .
        filter(strstarts(str(?lc), 'http://id.loc.gov/vocabulary/'))
        ?lc skos:prefLabel ?lcpreflabel .
    } union {
        ?s skos:exactMatch ?rda .
        filter(strstarts(str(?rda), 'http://rdaregistry.info/termList/'))
        ?rda skos:prefLabel ?rdapreflabel ;
            skos:definition ?rdadefinition .
    }
}
