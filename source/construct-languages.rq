prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix skos: <http://www.w3.org/2004/02/skos/core#>
prefix dc: <http://purl.org/dc/terms/>
prefix : <http://schema.org/>
prefix iso639_1: <http://id.loc.gov/vocabulary/iso639-1/>

construct {
    ?s a :Language;
        skos:prefLabel ?label ;
        skos:prefLabel ?prefLabel ;
        rdfs:comment ?comment ;
        skos:notation ?not;
        skos:closeMatch ?loclang ;
        skos:notation ?langCode;
        skos:closeMatch ?locvariant ;
        skos:notation ?langTag .

} where {
    graph <http://id.kb.se/dataset/languages> {
        {
            ?s rdfs:label ?label .
            optional { ?s rdfs:comment ?comment }
        } union {
            ?s skos:notation ?not .
            bind(strdt(?not, dc:ISO639-2) as ?langCode)
            bind(strdt(?not, xsd:string) as ?strnot)
            graph <http://id.loc.gov/vocabulary/languages> {
                optional {
                    ?loclang skos:notation ?strnot .
                    optional {
                        ?loclang skos:prefLabel ?prefLabel .
                        filter(langMatches(lang(?prefLabel), 'en'))
                    }
                    optional {
                        ?loclang skos:exactMatch ?locvariant .
                        filter exists { ?locvariant a iso639_1:iso639-1_Language }
                        ?locvariant skos:notation ?varnot .
                        bind(strdt(?varnot, dc:ISO639-1) as ?langTag)
                    }
                }
            }
        }
    }
}
