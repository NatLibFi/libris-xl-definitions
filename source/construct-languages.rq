prefix madsrdf: <http://www.loc.gov/mads/rdf/v1#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix skos: <http://www.w3.org/2004/02/skos/core#>
prefix dc: <http://purl.org/dc/terms/>
prefix kbv: <https://id.kb.se/vocab/>
prefix : <http://schema.org/>

construct {
    ?s ?p ?o ;
        skos:prefLabel ?prefLabel ;
        skos:altLabel ?altLabel ;
        skos:notation ?langTag, ?marc ;
        skos:exactMatch ?locLang ;
        skos:closeMatch ?locVariant .
} where {
    graph <https://id.kb.se/dataset/languages> {
        {
            ?s ?p ?o .
        } union {
            ?s skos:notation ?locCode .
            filter(
                datatype(?locCode) = kbv:ISO639-2-T ||
                datatype(?locCode) = xsd:string
            )
            bind(iri(concat('http://id.loc.gov/vocabulary/iso639-2/', str(?locCode))) as ?locLang)
            graph <http://id.loc.gov/vocabulary/languages> {
                {
                    filter(!contains(?prefLabel, "|"))
                    ?locLang madsrdf:authoritativeLabel|skos:prefLabel ?prefLabel
                } union {
                    filter(contains(?prefLabels, "|"))
                    ?locLang madsrdf:authoritativeLabel|skos:prefLabel ?prefLabels
                    bind(replace(?prefLabels, " *\\|.*$", "") as ?prefLabel)
                    bind(replace(?prefLabels, "^[^|]+\\| *", "") as ?altLabels)
                    values ?n { 1 2 3 4 5 6 7 }
                    bind(concat("^([^|]+\\|){", str(?n) ,"} *") as ?skipN)
                    bind(replace(replace(?altLabels, ?skipN, ""), " *\\|.*$", "") as ?altLabel)
                }
                optional {
                    filter(langMatches(lang(?prefLabel), 'en'))
                    ?locVariant madsrdf:authoritativeLabel|skos:prefLabel ?prefLabel .
                    bind('http://id.loc.gov/vocabulary/iso639-1/' as ?iso639_1_base)
                    filter(strstarts(str(?locVariant), ?iso639_1_base))
                    bind(substr(str(?locVariant), strlen(?iso639_1_base) + 1) as ?shortcode)
                    bind(strdt(?shortcode, kbv:ISO639-1) as ?langTag)
                }
            }
        }
    }
}
