prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix skos: <http://www.w3.org/2004/02/skos/core#>
prefix dc: <http://purl.org/dc/terms/>
prefix kbv: <https://id.kb.se/vocab/>
prefix : <http://schema.org/>

construct {
    ?s a :Language;
        skos:prefLabel ?label ;
        skos:prefLabel ?prefLabel ;
        skos:altLabel ?altLabel ;
        rdfs:comment ?comment ;
        skos:notation ?not;
        skos:notation ?langCode;
        kbv:ISO639-2-B ?bibliographicLangCode;
        kbv:ISO639-2-T ?termLangCode;
        kbv:ISO639-3 ?iso_639_3;
        dc:isReplacedBy ?replacement;
        skos:exactMatch ?locLang ;
        skos:closeMatch ?locVariant ;
        skos:notation ?langTag .

} where {
    graph <https://id.kb.se/dataset/languages> {
        {
            ?s skos:prefLabel ?label .
            optional { 
                filter(!contains(?altLabel, "|"))
                ?s skos:altLabel ?altLabel
            }
            optional { 
                filter(contains(?altLabels, "|"))
                ?s skos:altLabel ?altLabels
                values ?n { 0 1 2 3 4 5 }
                bind(concat("^([^|]+\\|){", str(?n) ,"} *") as ?skipN)
                bind(replace(replace(?altLabels, ?skipN, ""), " *\\|.*$", "") as ?altLabel)
            }
            optional { ?s rdfs:comment ?comment }
            optional { ?s kbv:ISO639-2-T ?termLangCode . }
            optional { ?s kbv:ISO639-3 ?iso_639_3 . }
        } union {
            {?s skos:notation ?locCode .} union {?s kbv:ISO639-2-T ?locCode .}  
            bind(iri(concat('http://id.loc.gov/vocabulary/iso639-2/', ?locCode)) as ?locLang)
            graph <http://id.loc.gov/vocabulary/languages> {
                optional {
                    filter(!contains(?prefLabel, "|"))
                    ?locLang skos:prefLabel ?prefLabel
                }
                optional {
                    filter(contains(?prefLabels, "|"))
                    ?locLang skos:prefLabel ?prefLabels
                    bind(replace(?prefLabels, " *\\|.*$", "") as ?prefLabel)
                    bind(replace(?prefLabels, "^[^|]+\\| *", "") as ?altLabels)
                    values ?n { 1 2 3 4 5 6 7 }
                    bind(concat("^([^|]+\\|){", str(?n) ,"} *") as ?skipN)
                    bind(replace(replace(?altLabels, ?skipN, ""), " *\\|.*$", "") as ?altLabel)
                }
                optional {
                    filter(langMatches(lang(?prefLabel), 'en'))
                    ?locVariant skos:prefLabel ?prefLabel .
                    bind('http://id.loc.gov/vocabulary/iso639-1/' as ?iso639_1_base)
                    filter(strstarts(str(?locVariant), ?iso639_1_base))
                    bind(substr(str(?locVariant), strlen(?iso639_1_base) + 1) as ?shortcode)
                    bind(strdt(?shortcode, dc:ISO639-1) as ?langTag)
                }
            }
        } union {
            optional { 
                ?s dc:isReplacedBy ?map_to_codes .
                values ?n { 0 1 2 3 }
                bind(concat("^([^|]+\\|){", str(?n) ,"} *") as ?skipN)
                bind(replace(replace(?map_to_codes, ?skipN, ""), " *\\|.*$", "") as ?map_to_code)
                ?replacement skos:notation ?map_to_code .
            }
        } union {
            ?s skos:notation ?not .
            bind(strdt(?not, dc:ISO639-2) as ?langCode)
        }
    }
}
